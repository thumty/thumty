apply from: './libraries.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    group 'org.eightlog'
    version '1.0.1'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    sourceSets {
        main {
            java {
                srcDirs += 'src/main/generated'
            }
        }
    }

    ext.platform = {
        if (project.hasProperty("platformName")) {
            return project.platformName
        }

        // Detect platform
        def os = System.getProperty("os.name").toLowerCase()
        def arch = System.getProperty("os.arch").toLowerCase()

        if (arch == 'amd64') {
            arch = 'x86_64'
        }

        if (os.startsWith("mac")) {
            return "macosx-" + arch
        }

        if (os.contains("win")) {
            return "windows-" + arch
        }

        if (os.contains("nux")) {
            return "linux-" + arch
        }

        throw new IllegalStateException("Unknown or unsupported OS: " + os)
    }

    dependencies {
        compile "com.google.guava:guava:${libVersions.guava}"
        compile "com.google.code.findbugs:jsr305:${libVersions.jsr305}"

        testCompile libraries.unit_test

        testCompile ("junit:junit:${libVersions.junit}") {
            exclude group: 'org.hamcrest'
        }

        testCompile ("org.mockito:mockito-core:${libVersions.mockito}") {
            exclude group: 'org.hamcrest'
        }
    }

    task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
        source = sourceSets.main.java
        classpath = configurations.compile
        destinationDir = project.file('src/main/generated')
        options.compilerArgs = [
                "-proc:only",
                "-processor", "io.vertx.codegen.CodeGenProcessor",
                "-AoutputDirectory=${project.projectDir}/src/main"
        ]
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

